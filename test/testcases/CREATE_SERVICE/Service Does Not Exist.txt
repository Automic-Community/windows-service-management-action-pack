*** Settings ***
Suite Setup       Suite Setup
Force Tags        create service
Test Template     Create Service Template
Resource          ../../resources/keywords.txt

*** Variables ***
${working_dir}    ${_FILE_WORKING_DIR}${/}FileCommand${/}TestWindowsService

*** Test Cases ***    SERVICE NAME             SERVICE PATH                SERVICE TYPE    DISPLAYED NAME           CREATE RETURN CODE    CHECK RETURN CODE
Create Service With Filesys Type
                      UC4.TestCreateService    DhmService.exe              filesys         UC4.TestCreateService    0                     0

Create Service With Kernel Type
                      UC4.TestCreateService    DhmService.exe              kernel          UC4.TestCreateService    0                     0

Create Service With Own Type
                      UC4.TestCreateService    DhmService.exe              own             UC4.TestCreateService    0                     0

Create Service With Share Type
                      UC4.TestCreateService    DhmService.exe              share           UC4.TestCreateService    0                     0

Create Service With Nonexist Path
                      UC4.TestCreateService    NONEXIST${/}NONEXIST.exe    own             NONEXIST                 0                     0

*** Keywords ***
Suite Setup
    Init AE And Package
    Init Win File Server

Create Service Template
    [Arguments]    ${serviceName}    ${servicePath}    ${serviceType}    ${serviceDisplayedName}    ${createReturnCode}    ${checkReturnCode}
    Init Action    ${ACTION_STOP_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Init Action    ${ACTION_DELETE_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Init Action    ${ACTION_CREATE_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Set    ${PROMPTSET_SERVICE_PATH}    ${working_dir}${/}${servicePath}
    Action Set    ${PROMPTSET_SERVICE_TYPE}    ${serviceType}
    Action Set    ${PROMPTSET_SERVICE_DISPLAY_NAME}    ${serviceDisplayedName}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${createReturnCode}
    Init Action    ${ACTION_CHECK_SERVICE_EXISTS}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${checkReturnCode}
    Action Report Should Contain    START AT
    #    Action Report Should Contain    PARAMETER VALIDATION START
    #    Action Report Should Contain    EXECUTION START
    #    Action Report Should Contain    ABEND AT
    #    Action Report Should Contain    RET-CODE
