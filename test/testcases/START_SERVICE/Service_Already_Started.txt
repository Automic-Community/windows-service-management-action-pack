*** Settings ***
Suite Setup       Suite Setup
Force Tags        start service
Test Template     Start Service Template
Resource          ../../resources/keywords.txt

*** Variables ***
${working_dir}    ${_FILE_WORKING_DIR}\\FileCommand\\WindowsServiceManager\\ServiceManager\\windows\\x64

*** Test Cases ***    SERVICE NAME       SERVICE PATH    SERVICE TYPE    DISPLAY NAME       RETURN CODE    RETURN STATUS                MESSAGE
Service Already Exists
                      UC4.TestService    UCYBSMgr.exe    own             UC4.TestService    1              ${ENDED_NOT_OK - aborted}    not in a STOPPED state. Won't issue a start

*** Keywords ***
Suite Setup
    Init AE And Package
    Init Win File Server

Start Service Template
    [Arguments]    ${serviceName}    ${servicePath}    ${serviceType}    ${serviceDisplayedName}    ${returnCode}    ${returnStatus}
    ...    ${message}
    Init Action    ${ACTION_STOP_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Init Action    ${ACTION_DELETE_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Init Action    ${ACTION_CREATE_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Set    ${PROMPTSET_SERVICE_PATH}    ${working_dir}${/}${servicePath}
    Action Set    ${PROMPTSET_SERVICE_TYPE}    ${serviceType}
    Action Set    ${PROMPTSET_SERVICE_DISPLAY_NAME}    ${serviceDisplayedName}
    Action Execute
    Init Action    ${ACTION_START_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Init Action    ${ACTION_START_SERVICE}
    Action Set    ${PROMPTSET_SERVICE_NAME}    ${serviceName}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${message}
    Action Report Should Contain    START AT
    #    Action Report Should Contain    PARAMETER VALIDATION START
    #    Action Report Should Contain    EXECUTION START
    #    Action Report Should Contain    ABEND AT
    #    Action Report Should Contain    RET-CODE
